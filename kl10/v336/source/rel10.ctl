;THIS IS A TOPS-10 CONTROL FILE FOR RELEASE OF KL10 MICROCODE,
;MODEL B - TOPS-10 MONITORS.
;THIS FILE FOR VERSION 336 RELEASE - RELEASE 702 OF TOPS-10

;FEBRUARY 24, 1984
.SET WATCH VERSION

.SET DSKFUL PAUSE		; STOP JOB ON DISK FULL


;SET UP SEARCH LIST TO GET ALL FILES FROM DEC: AND THEN DISK NO OTHER PLACES
.ASSIGN DEC SYS


;GET CHECKSUMS OF ALL RELEVENT FILES FOR ASSEMBLING BOTH MICROCODES
;ALL CHECKSUMS TAKEN BELOW MUST MATCH IN ORDER TO PROVE THE
;MICROCODE IS CORRECT

.R DIRECT
**.CCL/CHECK
**.MIC/CHECK
**.RNO/CHECK
**.MAC/CHECK
**.REL/CHECK
**.CTL/CHECK
**.RAM/CHECK
**.MCB/CHECK
*CONVRT.EXE/CHECK


;NOTE .EXE FILES CANNOT BE CHECKSUMMED IF THEY ARE REASSEMBLED THE
;CHECKSUMS WILL NOT MATCH IN ALL CASES THUS MICRO.EXE IS NOT CHECKSUMED



;ASSEMBLE THE MICROASSEMBLER

.LOAD MICRO.MAC/COMPILE

.SAVE MICRO.EXE

;GET THE CHECKSUM OF THE .REL FILE 
.R DIRECT
*MICRO.REL/CHECK


;ASSEMBLE TOPS-10 MODEL B MICROCODE
.RUN MICRO.EXE
*@UB



;NOW DO THE FIRST STEP OF THE CONVERSION PROCESS

;BECAUSE TOPS-10 CAN NOT RUN .EXE FILES WRITTEN ON TOPS-20
;THIS STEP MUST BE DONE
.GET CONVRT.EXE
.SAVE CONVRT.SAV
;END OF STEP WHEN IT IS REMOVED THE CONVRT.SAV RUNS
;BELOW MUST BE CHANGED TO CONVRT.EXE


;TOPS-10 MODEL B
.RUN CONVRT.SAV
*UB/R




;NOW DO THE SECOND STEP OF CONVERSION THE RSXT10 IS A SUPPORTED TOPS-10
;PROGRAM AND SHOULD BE ON DEC:

;TOPS-10 MODEL B
.R RSXT10
CONVERT UB.RAM UB.MCB
EXIT



;THERE HAVE BEEN SOFTWARE CHANGES TO THE CONVRT PROGRAM TO MAKE IT
;HAVE NO DATE DEPENDENCIES THE RAM AND MCB FILES SHOULD THEREFORE CHECKSUM
;PROPERLY
.R DIRECT
**.RAM/CHECK
**.MCB/CHECK



;RUNOFF THE DOCUMENTATION
.R RUNOFF 
*UCODE.DOC=UCODE.RNO

;TAKE CHECKSUMS FOR THE RUNOFF FILE
.R DIRECT
*UCODE.DOC/CHECK


;DONE QUIT
